apply plugin: 'com.android.application'
apply from: '../config/quality/quality.gradle'
apply plugin: 'com.neenbedankt.android-apt'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"
    publishNonDefault true

    defaultConfig {
        applicationId "com.hitherejoe.bourbon"
        minSdkVersion 21
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "com.hitherejoe.androidtestcommon.runner.RxAndroidJUnitRunner"

        buildConfigField("String", "DRIBBBLE_API_URL",
                "\"${dribbbleApiUrl}\"")
        buildConfigField("String", "DRIBBBLE_ACCESS_TOKEN",
                "\"${accessToken}\"")
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        def commonTestDir = 'src/commonTest/java'
        test {
            java.srcDir commonTestDir
        }
        androidTest {
            java.srcDir commonTestDir
        }
    }

    //Needed because of this https://github.com/square/okio/issues/58
    lintOptions {
        warning 'InvalidPackage'
    }

    packagingOptions {
        exclude 'META-INF/maven/com.google.dagger/dagger/pom.properties'
        exclude 'META-INF/maven/com.google.dagger/dagger/pom.xml'
    }
}

dependencies {
    final SUPPORT_LIBRARY_VERSION = '23.3.0'
    final DAGGER_VERSION = '2.2'
    def daggerCompiler = "com.google.dagger:dagger-compiler:$DAGGER_VERSION"

    compile project(':CoreCommon')
    compile 'de.hdodenhof:circleimageview:2.0.0'
    compile "com.android.support:support-v4:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:recyclerview-v7:$SUPPORT_LIBRARY_VERSION"

    provided 'org.glassfish:javax.annotation:10.0-b28' //Required by Dagger2

    // Instrumentation test dependencies
    androidTestCompile project(':AndroidTestCommon')

    // APT dependencies
    apt daggerCompiler
}