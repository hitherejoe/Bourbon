apply plugin: 'com.android.library'
apply from: '../config/quality/quality.gradle'
apply plugin: 'com.neenbedankt.android-apt'

android {
    defaultPublishConfig "debug"

    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"

        buildConfigField("String", "DRIBBBLE_API_URL",
                "\"${dribbbleApiUrl}\"")
        buildConfigField("String", "DRIBBBLE_ACCESS_TOKEN",
                "\"${accessToken}\"")
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        def commonTestDir = 'src/commonTest/java'
        test {
            java.srcDir commonTestDir
        }
        androidTest {
            java.srcDir commonTestDir
        }
    }

    packagingOptions {
        exclude 'META-INF/maven/com.google.dagger/dagger/pom.properties'
        exclude 'META-INF/maven/com.google.dagger/dagger/pom.xml'
    }

    //Needed because of this https://github.com/square/okio/issues/58
    lintOptions {
        warning 'InvalidPackage'
    }

}

dependencies {
    final RETROFIT_VERSION = '2.0.0'
    final OKHTTP_VERSION = '3.2.0'
    final DAGGER_VERSION = '2.2'
    final HAMCREST_VERSION = '1.3'

    def daggerCompiler = "com.google.dagger:dagger-compiler:$DAGGER_VERSION"
    def jUnit = "junit:junit:4.12"
    def mockito = "org.mockito:mockito-core:1.10.5"

    compile "com.android.support:support-v4:${android_support_library_version}"
    compile "com.android.support:recyclerview-v7:${android_support_library_version}"
    compile "com.android.support:support-annotations:${android_support_library_version}"
    compile "com.android.support:design:${android_support_library_version}"
    compile "com.android.support:percent:${android_support_library_version}"
    compile "com.android.support:customtabs:${android_support_library_version}"

    compile "com.squareup.retrofit2:retrofit:$RETROFIT_VERSION"
    compile "com.squareup.retrofit2:converter-gson:$RETROFIT_VERSION"
    compile "com.squareup.retrofit2:adapter-rxjava:$RETROFIT_VERSION"
    compile "com.squareup.okhttp3:okhttp:$OKHTTP_VERSION"
    compile "com.squareup.okhttp3:logging-interceptor:$OKHTTP_VERSION"

    compile 'io.reactivex:rxandroid:1.1.0'
    compile 'io.reactivex:rxjava:1.1.1'
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'com.jakewharton.timber:timber:4.1.0'
    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile jUnit
    compile mockito

    compile  "com.google.dagger:dagger:$DAGGER_VERSION"
    provided 'org.glassfish:javax.annotation:10.0-b28' //Required by Dagger2

    // Unit tests dependencies
    testCompile jUnit
    testCompile mockito
    testCompile "org.hamcrest:hamcrest-core:$HAMCREST_VERSION"
    testCompile "org.hamcrest:hamcrest-library:$HAMCREST_VERSION"
    testCompile "org.hamcrest:hamcrest-integration:$HAMCREST_VERSION"
    testCompile 'org.robolectric:robolectric:3.0'

    // APT dependencies
    apt daggerCompiler
    testApt daggerCompiler
}
